apply plugin: 'com.android.application'

def isRunningOnTravis = System.getenv("CI") == "true"

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId 'website.petrov.noue'
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 9
        versionName '1.0'
        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        release {}
    }
    buildTypes {
        debug {
            minifyEnabled false
            zipAlignEnabled true
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            crunchPngs false
        }
        release {
            minifyEnabled true
            zipAlignEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility '1.8'
        targetCompatibility '1.8'
    }
    dataBinding {
        enabled = true
    }
    buildToolsVersion = '28.0.3'

    if (isRunningOnTravis) {
        signingConfigs.release.storeFile = file("../sign-last.jks")
        signingConfigs.release.storePassword = System.getenv("keystore_password")
        signingConfigs.release.keyAlias = System.getenv("keystore_alias")
        signingConfigs.release.keyPassword = System.getenv("keystore_alias_password")
    } else {
        def keystorePropertiesFile = rootProject.file('keystore.properties')
        def keystoreProperties = new Properties()
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

        signingConfigs.release.storeFile = file(keystoreProperties['STORE_FILE'])
        signingConfigs.release.storePassword = keystoreProperties['STORE_PASSWORD']
        signingConfigs.release.keyAlias = keystoreProperties['KEY_ALIAS']
        signingConfigs.release.keyPassword = keystoreProperties['KEY_PASSWORD']
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.activity:activity:1.0.0-alpha04'
    implementation 'androidx.annotation:annotation:1.0.1'
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha02'
    implementation 'androidx.collection:collection:1.1.0-alpha02'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.1.0-alpha01'
    implementation 'androidx.core:core:1.1.0-alpha04'
    implementation 'androidx.databinding:databinding-runtime:3.5.0-alpha04'
    implementation 'androidx.drawerlayout:drawerlayout:1.0.0'
    implementation 'androidx.fragment:fragment:1.1.0-alpha04'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0-alpha02'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.1.0-alpha02'
    implementation 'androidx.preference:preference:1.1.0-alpha03'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-alpha02'
    implementation 'androidx.room:room-runtime:2.1.0-alpha04'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0-alpha01'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0-alpha01'
    implementation 'androidx.viewpager:viewpager:1.0.0'
    implementation 'androidx.loader:loader:1.1.0-alpha01'

    implementation 'com.github.bumptech.glide:glide:4.9.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.8.0'

    implementation 'com.google.android.material:material:1.1.0-alpha03'
    implementation 'com.google.code.gson:gson:2.8.5'

    implementation 'com.squareup.okhttp3:okhttp:3.13.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'

    annotationProcessor 'androidx.room:room-compiler:2.1.0-alpha04'
    annotationProcessor 'androidx.databinding:databinding-compiler:3.5.0-alpha03'

    implementation 'org.jetbrains:annotations:17.0.0'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.3'
    debugImplementation 'com.squareup.leakcanary:leakcanary-support-fragment:1.6.3'

    debugImplementation 'com.facebook.stetho:stetho:1.5.0'
    debugImplementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'
}
